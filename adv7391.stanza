; When renaming the stanza package name below, also rename it in the folder stanza.proj
; See docs.jitx.com for help
#use-added-syntax(jitx)
defpackage components/adv7391:
  import core
  import collections
  import jitx
  import jitx/commands
  import ocdb/utils/defaults
  import ocdb/utils/landpatterns
  import ocdb/utils/box-symbol
  import ocdb/utils/bundles
  import ocdb/utils/generic-components

pcb-landpattern lp-adv7391 :

  make-qfn-landpattern(pin-pitch, lead-span, n-pins, pad-width, pad-length, corner-pads) where :
    val pin-pitch = 0.5
    val lead-span = 5.0
    val n-pins = 32
    val pad-width = 0.2
    val pad-length = 0.4
    val corner-pads = false

  pad p[33] : smd-pad(3.1, 3.1) at loc(0.0, 0.0)  

public pcb-component component :
  name = "adv7391"
  description = "3 DAC output analog video encoder"
  mpn = "ADV7391"
  manufacturer = "Analog Devices"
  reference-prefix = "U"

  pin-properties :
    [pin:Ref      | pads:Int ... | side:Dir ]

    [P7           | 9            | Left     ]
    [P6           | 8            | Left     ]
    [P5           | 7            | Left     ]
    [P4           | 4            | Left     ]
    [P3           | 3            | Left     ]
    [P2           | 2            | Left     ]
    [P1           | 31           | Left     ]
    [P0           | 30           | Left     ]

    [DAC1         | 22           | Right    ]
    [DAC2         | 21           | Right    ]
    [DAC3         | 20           | Right    ]

    [CLKIN        | 13           | Left     ]
    [nHSYNC       | 27           | Right    ]
    [nVSYNC       | 26           | Right    ]
    [SFL          | 25           | Right    ]
    [RSET         | 24           | Left     ]
    [COMP         | 23           | Left     ]
    [SCL          | 12           | Left     ]
    [SDA          | 11           | Left     ]
    [ALSB         | 10           | Left     ]
    [nRESET       | 14           | Left     ]

    [EXT_LF       | 16           | Left     ]

    [VAA          | 19           | Left     ]
    [VDD          | 5, 28        | Left     ]
    [VDDIO        | 1            | Left     ]
    [PVDD         | 17           | Left     ]

    [PGND         | 15           | Right    ]
    [AGND         | 18           | Right    ]
    [DGND         | 6, 29        | Right    ]
    [GND_IO       | 32           | Right    ]
    [EPAD         | 33           | Right    ]

  make-box-symbol()
  assign-landpattern(lp-adv7391)

  ; supports power:
  ;   power.vdd => self.VCC
  ;   power.gnd => self.GND


public pcb-module adv7391-module () :
  inst adv : components/adv7391/component

  pin p1v8
  pin p3v3
  pin vddio
  pin gnd

  pin nreset
  net (nreset, adv.nRESET)

  pin clkin
  net (clkin, adv.CLKIN)

  port din : pin[8]
  net (din[0], adv.P0)
  net (din[1], adv.P1)
  net (din[2], adv.P2)
  net (din[3], adv.P3)
  net (din[4], adv.P4)
  net (din[5], adv.P5)
  net (din[6], adv.P6)
  net (din[7], adv.P7)

  pin scl
  net (scl, adv.SCL)
  pin sda
  net (sda, adv.SDA)

  pin dac1
  net (dac1, adv.DAC1)
  pin dac2
  net (dac2, adv.DAC2)
  pin dac3
  net (dac3, adv.DAC3)

  pin alsb
  net (alsb, adv.ALSB)

  pin nhsync
  net (nhsync, adv.nHSYNC)

  pin nvsync
  net (nvsync, adv.nVSYNC)

  pin sfl
  net (sfl, adv.SFL)

  net v1V8 (p1v8, adv.VDD, adv.PVDD)
  net v3V3 (p3v3, adv.VAA)
  net VDDIO (vddio, adv.VDDIO)
  net GND (gnd, adv.PGND, adv.AGND, adv.DGND, adv.GND_IO, adv.EPAD)

  ; as per dev kit
  cap-strap(adv.COMP, v3V3, 2.2e-9)
  res-strap(adv.RSET, GND, 510.0)

  cap-strap(adv.EXT_LF, v1V8, 12.0e-9)
  net filt_mid ()
  cap-strap(v1V8, filt_mid, 150.0e-9)
  res-strap(adv.EXT_LF, filt_mid, 178.0)

  ; output terminators
  res-strap(adv.DAC1, GND, 75.0)
  res-strap(adv.DAC2, GND, 75.0)
  res-strap(adv.DAC3, GND, 75.0)

  ; decoupling
  cap-strap(VDDIO, GND, 10.0e-9)
  cap-strap(VDDIO, GND, 100.0e-9)

  cap-strap(v1V8, GND, 10.0e-9)
  cap-strap(v1V8, GND, 10.0e-9)
  cap-strap(v1V8, GND, 10.0e-9)
  cap-strap(v1V8, GND, 100.0e-9)
  cap-strap(v1V8, GND, 100.0e-9)
  cap-strap(v1V8, GND, 100.0e-9)

  cap-strap(v3V3, GND, 10.0e-9)
  cap-strap(v3V3, GND, 100.0e-9)
  cap-strap(v3V3, GND, 1.0e-6)